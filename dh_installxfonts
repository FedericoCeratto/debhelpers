#!/usr/bin/perl -w
#
# Integration with the Debian X11 font policy.

use strict;
use Debian::Debhelper::Dh_Lib;
init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $XFONTDIR="$tmp/usr/X11R6/lib/X11/fonts/";

	# Find all fint directories in the package build directory.
	opendir DIR, $XFONTDIR || next;
	my @fontdirs = grep { -d "$XFONTDIR/$_" && !/^\./ } (readdir DIR);
	closedir DIR;

	if (@fontdirs) {
		# Figure out what commands the postinst will need to call.
		my @updatecmds=('/usr/bin/X11/mkfontdir');
		foreach my $f (@fontdirs) {
			push @updatecmds, '/usr/sbin/update-fonts-alias'
				if -f "$tmp/etc/X11/fonts/$f/$package.alias";
			# This must come _before_ mkfontdir, thus the unshift.
			unshift @updatecmds, '/usr/sbin/update-fonts-scale'
				if -f "$tmp/etc/X11/fonts/$f/$package.scale";
		}

		autoscript($package, "postinst", "postinst-xfonts",
			"s:#FONTDIRS#:".join(' ', @fontdirs).
			":;s:#UPDATECMDS#:".join(' ', @updatecmds).":");
		autoscript($package, "postrm", "postrm-xfonts",
			"s:#FONTDIRS#:".join(' ', @fontdirs).
			":;s:#UPDATECMDS#:".join(' ', @updatecmds).":");
	}
}
