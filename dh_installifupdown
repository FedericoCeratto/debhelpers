#!/usr/bin/perl -w

=head1 NAME

dh_installifupdown - install if-up and if-down hooks

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installifupdown> [S<I<debhelper options>>] [B<--name=>I<name>]

=head1 DESCRIPTION

dh_installifupdown is a debhelper program that is responsible for installing
if-up, if-down, if-pre-up, and if-post-down hook scripts into package build
directories.

If a file named debian/package.if-up exists, then it is installed into
etc/network/if-up.d/package in the package build directory. Similar is done
for if-down, if-pre-up, and if-post-down.

=head1 OPTIONS

=over 4

=item B<--name=>I<name>

Look for files named debian/package.name.if-* and install them as
etc/network/if-*/name, instead of using the usual files and installing them
as the package name.

=back

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	
	foreach my $script (qw(pre-up up down post-down)) {
		my $file=pkgfile($package, "if-$script");
		if ($file ne '') {
			if (! -d "$tmp/etc/network/if-$script.d") {
				doit("install","-d","$tmp/etc/network/if-$script.d");
			}
			doit("install","-p","-m755",$file,"$tmp/etc/network/if-$script.d/".pkgfilename($package));
		}
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
