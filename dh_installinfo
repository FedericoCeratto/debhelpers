#!/usr/bin/perl -w

=head1 NAME

dh_installinfo - install info files

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installinfo> [S<I<debhelper options>>] [B<-A>] [S<I<file ...>>]

=head1 DESCRIPTION

dh_installinfo is a debhelper program that is responsible for installing
info files into usr/share/info in the package build directory.

Any filenames specified as parameters will be installed into the first
package dh_installinfo is told to act on. By default, this is the first
binary package in debian/control, but if you use -p, -i, or -a flags, it
will be the first package specified by those flags.

Files named debian/package.info can list other files to be installed.

=head1 OPTIONS

=over 4

=item B<-A>, B<--all>

Install all files specified by command line parameters in ALL packages
acted on.

=item I<file ...>

Install these info files into the first package acted on. (Or in
all packages if -A is specified).

=back

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $file=pkgfile($package,"info");

	my @info;
	
	if ($file) {
		@info=filearray($file, ".");
	}

	if (($package eq $dh{FIRSTPACKAGE} || $dh{PARAMS_ALL}) && @ARGV) {
		push @info, @ARGV;
	}

	if (@info) {
		if ( ! -d "$tmp/usr/share/info") {
			doit("install","-d","$tmp/usr/share/info");
		}
		doit("cp",@info,"$tmp/usr/share/info");
		doit("chmod","-R", "go=rX","$tmp/usr/share/info/");
		doit("chmod","-R", "u+rw","$tmp/usr/share/info/");
		# For transition, could be removed later.
		addsubstvar($package, "misc:Depends", "dpkg (>= 1.15.4) | install-info");
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
