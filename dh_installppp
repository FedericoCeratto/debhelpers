#!/usr/bin/perl -w

=head1 NAME

dh_installppp - install ppp if-up and if-down files

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installppp> [S<I<debhelper options>>] [B<--name=>I<name>]

=head1 DESCRIPTION

dh_installppp is a debhelper program that is responsible for installing
ppp if-up and if-down scripts. into package build directories.

If a file named debian/package.ppp.ip-up exists, then it is installed into
etc/ppp/ip-up.d/package in the package build directory. 
Files named debian/package.ppp.ip-down are installed to
etc/ppp/ip-down.d/package

=head1 OPTIONS

=over 4

=item B<--name=>I<name>

Look for files named debian/package.name.ppp.ip-* and install them as
etc/ppp/ip-*/name, instead of using the usual files and installing them
as the package name.

=back

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	
	foreach my $script (qw(up down)) {
		my $file=pkgfile($package, "ppp.ip-$script");
		if ($file ne '') {
			if (! -d "$tmp/etc/ppp/ip-$script.d") {
				doit("install","-d","$tmp/etc/ppp/ip-$script.d");
			}
			doit("install","-p","-m755",$file,"$tmp/etc/ppp/ip-$script.d/".pkgfilename($package));
		}
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
