#!/usr/bin/perl -w

=head1 NAME

dh_installdebconf - install files used by debconf in package build directories

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installdebconf> [S<I<debhelper options>>] [B<-n>] [S<B<--> I<params>>]

=head1 DESCRIPTION

dh_installdebconf is a debhelper program that is responsible for installing
files used by the debconf package into package build directories.

It also automatically generates the postrm commands needed to
interface with debconf. See L<dh_installdeb(1)> for an explanation of how
that works.

Files named debian/package.config and debian/package.templates are
installed into the DEBIAN directory in the package build directory.

Note that if you use debconf, your package probably needs to depend on it
(it will be added to ${misc:Depends by this program).

=head1 LOCALIZED TEMPLATE FILES

Debconf also supports localized template files, and this program has some
support to aid working with them. It is best to keep the translations in
separate files, and merge them only at build time. See 
L<debconf-mergetemplate(1)> and L<debconf-getlang(1)> for details.

This program will automatically call debconf-mergetemplate and merge 
templates on the fly if it finds your template files are accompanied
by translated files that have the same name as the template file, with a
dot and a locale name prepended.

For example, if you have a German translation,
debian/package.templates.de is merged with debian/package.templates.

=head1 OPTIONS

=over 4

=item B<-n>, B<--noscripts>

Do not modify postrm script.

=item B<--> I<params>

Pass the params to debconf-mergetemplate.

=back

=cut

init();

my @extraparams;
if (defined($dh{U_PARAMS})) {
	@extraparams=@{$dh{U_PARAMS}};
}

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $config=pkgfile($package,"config");
	my $templates=pkgfile($package,"templates");

	if (! -d "$tmp/DEBIAN") {
		doit("install","-o",0,"-g",0,"-d","$tmp/DEBIAN");
	}

	if ($config ne '') {
		doit("install", "-o", 0, "-g", 0, "-m", 755, "-p",
		     $config, "$tmp/DEBIAN/config");
	}
	
	if ($templates ne '') {
		# Are there translated templates too?
		my @trans=(glob("$templates.??"), glob("$templates.??_??"));
		if (@trans) {
			complex_doit("debconf-mergetemplate --drop-old-templates @extraparams @trans $templates > $tmp/DEBIAN/templates");
			chmod 0644, "$tmp/DEBIAN/templates";
			chown 0, 0, "$tmp/DEBIAN/templates";
		}
		else {
			doit("install", "-o", 0, "-g", 0, "-m", 644, "-p",
			     $templates, "$tmp/DEBIAN/templates");
		}
	}

	if ($config ne '' || $templates ne '') {
		# I'm going with debconf 0.5 because it was the first
		# "modern" one.
		addsubstvar($package, "misc:Depends", "debconf", ">= 0.5");
	}
	else {
		addsubstvar($package, "misc:Depends", "debconf", ">= 0.5", 1); # remove
	}
	
	if (($config ne '' || $templates ne '') && ! $dh{NOSCRIPTS}) {
		autoscript($package,"postrm","postrm-debconf");
	}
}

=head1 SEE ALSO

L<debhelper(1)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
