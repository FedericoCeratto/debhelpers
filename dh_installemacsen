#!/usr/bin/perl -w
#
# Registration with emacsen-common.

use strict;
use Debian::Debhelper::Dh_Lib;
init();

if (! defined $dh{PRIORITY}) {
	$dh{PRIORITY}=50;
}
if (! defined $dh{FLAVOR}) {
	$dh{FLAVOR}='emacs';
}

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);

	my $emacsen_install=pkgfile($package,"emacsen-install");
	my $emacsen_remove=pkgfile($package,"emacsen-remove");
	my $emacsen_startup=pkgfile($package,"emacsen-startup");

	if ($emacsen_install ne '') {
		if (! -d "$tmp/usr/lib/emacsen-common/packages/install") {
			doit("install","-d","$tmp/usr/lib/emacsen-common/packages/install");
		}
		doit("install","-m0755",$emacsen_install,"$tmp/usr/lib/emacsen-common/packages/install/$package");
	}

	if ($emacsen_remove ne '') {
		if (! -d "$tmp/usr/lib/emacsen-common/packages/remove") {
			doit("install","-d","$tmp/usr/lib/emacsen-common/packages/remove");
		}
		doit("install","-m0755","$emacsen_remove","$tmp/usr/lib/emacsen-common/packages/remove/$package");
	}
	
	if ($emacsen_startup ne '') {
		if (! -d "$tmp/etc/$dh{FLAVOR}/site-start.d/") {
			doit("install","-d","$tmp/etc/$dh{FLAVOR}/site-start.d/");
		}
		doit("install","-m0644",$emacsen_startup,"$tmp/etc/$dh{FLAVOR}/site-start.d/$dh{PRIORITY}$package.el");
	}

	if ($emacsen_install ne '' || $emacsen_remove ne '') {
		if (! $dh{NOSCRIPTS}) {
			autoscript($package,"postinst","postinst-emacsen",
				"s/#PACKAGE#/$package/");
			autoscript($package,"prerm","prerm-emacsen",
				"s/#PACKAGE#/$package/");
		}
	}
}	
