#!/usr/bin/perl -w

=head1 NAME

dh_installmime - install mime files into package build directories

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

  dh_installmime [debhelper options] [-n]

=head1 DESCRIPTION

dh_installmime is a debhelper program that is responsible for installing
mime files into package build directories.

It also automatically generates the postinst and postrm commands needed to
interface with the debian mime-support package. See L<dh_installdeb(1)> for
an explanation of how this works.

If a file named debian/package.mime exists, then it is installed into
usr/lib/mime/packages/package in the package build directory.

=head1 OPTIONS

=over 4

=item B<-n>, B<--noscripts>

Do not modify postinst/postrm scripts.

=back

=head1 NOTES

Note that this command is not idempotent. "dh_clean -k" should be called
between invocations of this command. Otherwise, it may cause multiple
instances of the same text to be added to maintainer scripts.

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $mime=pkgfile($package,"mime");
	
	if ($mime ne '') {
		if (! -d "$tmp/usr/lib/mime/packages") {
			doit("install","-d","$tmp/usr/lib/mime/packages");
		}
		doit("install","-p","-m644",$mime,"$tmp/usr/lib/mime/packages/$package");

		if (! $dh{NOSCRIPTS}) {
			autoscript($package,"postinst","postinst-mime");
			autoscript($package,"postrm","postrm-mime")
		}
	}
}

=head1 SEE ALSO

L<debhelper(1)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
