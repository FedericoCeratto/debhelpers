#!/usr/bin/perl -w

=head1 NAME

dh_installinit - install init scripts into package build directories

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

  dh_installinit [debhelper options] [--init-script=scriptname] 
                 [-n] [-r] [-d] [-uparams] -- [params]

=head1 DESCRIPTION

dh_installinit is a debhelper program that is responsible for installing
init scripts and associated defaults files into package build directories.

It also automatically generates the postinst and postrm and prerm commands
needed to set up the symlinks in /etc/rc*.d/ and to start and stop the init
scripts.

If a file named debian/package.init exists, then it is installed into
etc/init.d/package in the package build directory, with "package" replaced
by the package name.

If a file named debian/package.default exists, then it is installed into
etc/default/package in the package build directory, with "package" replaced
by the package name.

=head1 OPTIONS

=over 4

=item B<-n>, B<--noscripts>

Do not modify postinst/postrm/prerm scripts.

=item B<-r>, B<--no-restart-on-upgrade>

Do not restart daemon on upgrade.

=item B<-d>, B<--remove-d>

Remove trailing "d" from the name of the package, and use the result for the
filename the init script is installed as in etc/init.d/ , and the default file
is installed as in etc/default/ . This may be useful for daemons with names
ending in "d". (Note: this takes precedence over the --init-script parameter
described below.)

=item B<-u>I<params> B<--update-rcd-params=>I<params>

=item B<--> I<params>

Pass "params" to L<update-rc.d(8)>. If not specified, "defaults" will be
passed to L<update-rc.d(8)>.

=item B<--init-script=>I<scriptname>

Use "scriptname" as for the filename the init script is installed as in
etc/init.d/ (and also use it as the filename for the defaults file, if it
is installed). This is useful if you need to have an init script with a name
different from the package's name. Note that if you use this parameter,
dh_installinit will look to see if a file in the debian/ directory exists
that looks like "package.scriptname" and if so will install it as the init
script in preference to the files it normally installs. This feature is really
only useful if you need a single package to install more than one init script.

=back

=head1 NOTES

Note that this command is not idempotent. "dh_clean -k" should be called
between invocations of this command. Otherwise, it may cause multiple
instances of the same text to be added to maintainer scripts.

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);

	# Figure out what filename to install it as.
	my $script;
	if ($dh{D_FLAG}) {
		# -d on the command line sets D_FLAG. We will 
		# remove a trailing 'd' from the package name and 
		# use that as the name.
		$script=$package;
		if ($script=~m/(.*)d$/) {
			$script=$1;
		}
		else {
			warning("\"$package\" has no final d' in its name, but -d was specified.");
		}
	}       
	elsif ($dh{INIT_SCRIPT}) {
		$script=$dh{INIT_SCRIPT};
	}
	else {
		$script=$package;
	}       

	my $init=pkgfile($package,$script) || pkgfile($package,"init") ||
	      pkgfile($package,"init.d");
	my $default=pkgfile($package,'default');

	if ($default ne '') {
		if (! -d "$tmp/etc/default") {
			doit("install","-d","$tmp/etc/default");
		}
		doit("install","-p","-m644",$default,"$tmp/etc/default/$script");
	}

	if ($init ne '') {
		if (! -d "$tmp/etc/init.d") {
			doit("install","-d","$tmp/etc/init.d");
		}

		doit("install","-p","-m755",$init,"$tmp/etc/init.d/$script");

		# This is set by the -u "foo" command line switch, it's
		# the parameters to pass to update-rc.d. If not set,
		# we have to say "defaults".
		my $params='';
		if (defined($dh{U_PARAMS})) {
			$params=join(' ',@{$dh{U_PARAMS}});
		}	
		if ($params eq '') {
			$params="defaults";
		}

		if (! $dh{NOSCRIPTS}) {
			# -r on the command line sets R_FLAG. If it's set, there
			# is no restart on upgrade.
			if ($dh{R_FLAG}) {
				autoscript($package,"postinst", "postinst-init-norestart",
					"s/#SCRIPT#/$script/;s/#INITPARMS#/$params/");
				autoscript($package,"postrm","postrm-init",
					"s/#SCRIPT#/$script/;s/#INITPARMS#/$params/");
				autoscript($package,"prerm","prerm-init-norestart",
					"s/#SCRIPT#/$script/;s/#INITPARMS#/$params/");
			}
			else {
				autoscript($package,"postinst","postinst-init",
					"s/#SCRIPT#/$script/;s/#INITPARMS#/$params/");
				autoscript($package,"postrm","postrm-init",
					"s/#SCRIPT#/$script/;s/#INITPARMS#/$params/");
				autoscript($package,"prerm","prerm-init",
					"s/#SCRIPT#/$script/;s/#INITPARMS#/$params/");
			}
		}
	}
}

=head1 SEE ALSO

L<debhelper(1)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
