#!/usr/bin/perl -w

=head1 NAME

dh_installpam - install pam support files

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installpam> [S<I<debhelper options>>] [B<-n>]

=head1 DESCRIPTION

dh_installpam is a debhelper program that is responsible for installing
files used by PAM into package build directories.

If a file named debian/package.pam exists, then it is installed into
etc/pam.d/package in the package build directory.

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $pam=pkgfile($package,"pam");
	
	if ($pam ne '') {
		if (! -d "$tmp/etc/pam.d") {
			doit("install","-d","$tmp/etc/pam.d");
		}
		doit("install","-p","-m644",$pam,"$tmp/etc/pam.d/$package");
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
