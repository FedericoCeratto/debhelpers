#!/usr/bin/perl -w
#
# Passed a list of undocumented man pages, generates symlinks to
# undocumented.7.gz for those man pages.
#
# Also, it looks for debian/undocumented files for more lists of
# undocumented man pages.

use strict;
use Debian::Debhelper::Dh_Lib;
init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $undocumented=pkgfile($package,"undocumented");

	my @undoc;
	if ($undocumented) {
		@undoc=filearray($undocumented);
	}

	if (($package eq $dh{FIRSTPACKAGE} || $dh{PARAMS_ALL}) && @ARGV) {
		push @undoc, @ARGV;
	}	

	foreach my $file (@undoc) {
		$file=~s/.gz$//; # .gz extension is optional in input.

		# Determine what directory the file belongs in,
		# /usr/share/man, or /usr/X11R6/man, and how the link to
		# the undocuemtned.7 man page will look.
		my ($dir, $reldir);
		my ($section)=$file=~m/^.*\.(\d)/;
		if (!$section) {
			error("\"$file\" does not have an extension.");
		}	
		if ($file=~/.*\.\dx/) {
			$dir="usr/X11R6/man/man$section";
			$reldir="../../../share/man/man7/";
		}
		elsif ($section != 7) {
			$dir="usr/share/man/man$section";
			$reldir="../man7/";
		}
		else {
			$dir="usr/share/man/man$section";
			$reldir="";
		}

		if (! -d "$tmp/$dir") {
			doit("install","-d","$tmp/$dir");
		}
		doit("ln","-sf","${reldir}undocumented.7.gz","$tmp/$dir/$file.gz");
	}
}
