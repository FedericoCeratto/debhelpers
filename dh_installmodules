#!/usr/bin/perl -w
#
# Register modules with modutils.

use strict;
use Debian::Debhelper::Dh_Lib;
use File::Find;
init();

# Returns true if there are any .o files in the passed directory.
sub find_kernel_modules {
	my $searchdir=shift;
	my @results=();

	return unless -d $searchdir;
	find(sub { push @results, $_ if /\.o$/ }, $searchdir);
	return @results > 0;
}

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $file=pkgfile($package,"modules");

	if (! -e $tmp) {
		doit("install","-d",$tmp);
	}

	if ($file) {
		if (! -e "$tmp/etc/modutils") {
			doit("install","-d","$tmp/etc/modutils");
		}
		doit("install","-m","0644",$file,"$tmp/etc/modutils/$package");
	}

	if (! $dh{NOSCRIPTS} &&
	    ($file || find_kernel_modules("$tmp/lib/modules"))) {
			autoscript($package,"postinst","postinst-modules","s/#PACKAGE#/$package/");
			autoscript($package,"postrm","postrm-modules","s/#PACKAGE#/$package/");
	}
}
